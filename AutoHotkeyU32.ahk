;;THIS SCRIPT IS FOR BOTH WINDOWS XP & WINDOWS 10.  AUTOHOTKEY-v1.1.  
;;HOTKEYS:  LockWorkStation RWIN  RENAME !r  EXPLORER !e  TRANSPARENCY #t  DUPLICATE ^d  DVORAK ^!+d  ALWAYSONTOP ^SPACE  SUSPEND (^!SPACE DOUBLE-TAP)  TITLEBAR F12  GRIP ^F12  WINDOW-LIST F11  HIDDEN-WINDOWS +F11  .
;;THIS SCRIPT GENERALLY EMULATES CTRL WITH ALT (!c/!v FOR COPY/PASTE, ETC). MOUSE TOO!
;;~200 LINES & ~1000 WORDS.  THIS SCRIPT IS STRIPPED OF COMMENTS AND SENT TO \.rsrc\RCDATA\1  (OPEN .EXE IN 7-ZIP).

#SINGLEINSTANCE
DVORAK = TRUE  ;SWITCH TO FALSE FOR NO DVORAK BY DEFAULT.  V2 REQUIRES :=   MUST USE " NOT '.  

MENU TRAY,TIP,FOR WINDOWS XP/10
MENU TRAY,MAINWINDOW ;FOR BUILD
MENU TRAY,ADD        ;DIVIDERS. 
MENU TRAY,ADD
MENU TRAY,ADD,ALWAYSONTOP TOGGLE  `t ^SPACE,ALWAYSONTOP_TOGGLE ;A MONOSPACE FONT WOULD BE PREFERRED, LIKE CONSOLAS.  
MENU TRAY,ADD,TITLEBAR TOGGLE     `t  F12  ,TITLEBAR_TOGGLE
MENU TRAY,ADD,GRIP TOGGLE         `t ^F12  ,GRIP_TOGGLE        ;THE GRIP ALLOWS RESIZING WITHOUT ANNOYING TITLEBAR. BUT IT CHANGES HORIZONTAL SIZING, SLIGHTLY.
MENU TRAY,ADD,TRANSPARENCY TOGGLE `t #t    ,TRANSPARENCY_TOGGLE
MENU TRAY,ADD,DVORAK TOGGLE       `t !^+d  ,DVORAK_TOGGLE      ;CHECKBOX.
MENU TRAY,ADD
MENU TRAY,ADD,WINGETLIST          `t  F11  , F11               ;SIMPLE LIST OF WINDOWS.
MENU TRAY,ADD,WINGETLIST`, FULL   `t +F11  ,+F11               ;FULL LIST.
MENU TRAY,ADD                              
MENU TRAY,ADD,LockWorkStation     `t RWIN  ,LockWorkStation

DVORAK_TOGGLE(){  ;@MENU & @!^+d.
    GLOBAL DVORAK := !DVORAK
    MENU TRAY,TOGGLECHECK,DVORAK TOGGLE       `t !^+d  
}

LockWorkStation(){  ;@MENU.
    DLLCALL("LockWorkStation")  ;DOESN'T GENERALIZE TO LINUX, BUT #l IS DODGY.  USER32.DLL ENTRIES ARE CASE SENSITIVE. 
    ; RUN RUNDLL32 USER32 LockWorkStation ;ALTERNATIVE CMD TO LOCK, DIRECTING RUNDLL32.EXE.
}

ALTTAB(){  ;@ALWAYSONTOP_TOGGLE, @TITLEBAR_TOGGLE, @GRIP_TOGGLE & @TRANSPARENCY_TOGGLE.  
    SEND !{TAB}{ALTTABMENUDISMISS}   ;UNFORTUNATELY THIS IS STILL A GLITCH (ALTTAB MENU).  THIS RESTORES A MINIMIZED WIN IF THEY'RE ALL MINIMIZED, FOR SIMPLICITY.
    WINWAITACTIVE ,,,,Task Switching ;FILTERS OUT Task Switching.  THIS DOESN'T WAIT LONG ENOUGH ON SOME WINDOWS.
}

ALWAYSONTOP_TOGGLE(){  ;@MENU.
    ALTTAB()
    WINSET ALWAYSONTOP,TOGGLE,A ;A IS ACTIVE WINDOW.
}

TITLEBAR_TOGGLE(){  ;@MENU.
    ALTTAB()
    WINSET STYLE,^0XC40000,A  ;^ TOGGLES (BIT-FLIPS).  C BEATS 8 IN MY TESTS, BUT EITHER WORKS. 0XC00000==WS_CAPTION==WS_BORDER|WS_DLGFRAME   WS_SIZEBOX==WS_THICKFRAME TOGGLES SIZING.  0X800000 IS WS_BORDER. ^0X# IS XOR (BINARY FLAG TOGGLE).
}

GRIP_TOGGLE(){  ;@MENU.
    ALTTAB()
    WINSET STYLE, ^0X40000,A
}

TRANSPARENCY_TOGGLE(ITEMNAME:=FALSE){  ;@MENU & *t.  
    IF     ITEMNAME 
           ALTTAB()
    WINGET ALPHA,TRANSPARENT,A
    IF     ALPHA
           ALPHA = OFF
    ELSE   ALPHA = 128
    WINSET TRANSPARENT,%ALPHA%,A 
}

DUPLICATE(){  ;@*d & @*h.  DUPLICATE OUTSIDE NOTEPAD++.  
    WINGET   PROCESSNAME,PROCESSNAME,A
    IF INSTR(PROCESSNAME,"NOTEPAD++") ;NOT CASE SENSITIVE.
        SEND ^D                       ;LINEDUPLICATE INSIDE NPP.  ^d ISN'T AS GOOD.
    ELSE {
        SEND ^c^v  ;EXPLORER PASTES ONCE ONLY.  2 SENDS REQUIRED FOR RELIABILITY, OR ELSE USE CUT, NOT COPY.
        IF NOT INSTR(PROCESSNAME,"EXPLORER")  
            SEND ^v
}}

F11::  ;WINDOW LIST.  V2 HAS CONTROLLIST (^F11).  ALSO HAS SIZE, COORDS, MIN/MAX, WID & PROCESS PATH.
    MSG = 
    DETECTHIDDENWINDOWS ON  
    WINGET FULLCOUNT   ,COUNT  ;HIDDEN COUNT.  ~41 IN WIN-XP.
    DETECTHIDDENWINDOWS OFF
    WINGET LIST        ,LIST
    WINGET COUNT       ,COUNT 
    LOOP  %COUNT% 
    {
        ahk_id :=  "ahk_id " . LIST%A_INDEX%  ;:= EVALUATES.  LOWERCASE FOR LINUX.  
        WINGET      PID        ,PID        ,%ahk_id% 
        WINGET      PROCESSNAME,PROCESSNAME,%ahk_id% 
        WINGETCLASS CLASS                  ,%ahk_id%
        WINGETTITLE WINTITLE               ,%ahk_id%
        MSG = %MSG%PID=%PID%  %PROCESSNAME%  CLASS=%CLASS%  TITLE=%WINTITLE%`n`n
    }
    
    MSGBOX ,,%COUNT% WINDOWS + %FULLCOUNT%-%COUNT% HIDDEN,%MSG%
    RETURN

+F11::  ;HIDDEN WINDOW LIST. 
    MSG = 
    DETECTHIDDENWINDOWS ON
    WINGET LIST ,LIST
    WINGET COUNT,COUNT 
    LOOP  %COUNT% 
    {
        ahk_id := "ahk_id " . LIST%A_INDEX%
        WINGETCLASS CLASS,%ahk_id%
        MSG = %MSG%  %A_INDEX% %CLASS%  ;TOO MANY.
    }
    MSGBOX ,,%COUNT% WINDOWS TOTAL. CLASSES BELOW.,%MSG%
    RETURN

   ^!SPACE::SUSPEND ;IMMUNE TO SUSPENSION.  ^ IS CTRL.
    !SPACE::        ;BOTH (LALT & CTRL)&SPACE DO SAME THING.
    ^SPACE::WINSET ALWAYSONTOP,TOGGLE,A
    ~*RWIN::l       ;~ PASSES #.  RWIN IS >#.  * (WILDCARD MOD) IS NECESSARY FOR SHIFT+RWIN TO TRIGGER, ALONG WITH OTHER MODS.  RWIN-LOCK CAN BE ANNOYING IF A PASSWORD IS LONG OR UNKNOWN. BUT IT COULD ALSO PREVENT SEIZURE OF A SYSTEM WHICH IS TOO DIFFICULT TO LOCK.

  !WHEELUP::^WHEELUP         ;MOUSE SECTION.  !MOUSE=^MOUSE USUALLY.  NONE OF THESE WORK IN LINUX.  ALT-ZOOM.   WHEELUP/DOWN DON'T NEED SEND.
!WHEELDOWN::^WHEELDOWN
  ~WHEELUP::                 ;BUGFIX FOR WHEELUP/DOWN AFTER !WHEELUP/DOWN.  ~ MEANS PASSTHROUGH.
~WHEELDOWN::RETURN           
  !LBUTTON::SEND  ^{LBUTTON} ;ALT FILE SELECTIONS, LIKE CTRL. COMPATIBLE WITH EXPLORER & NOTEPAD++.  RUINS SQUARE SELECTIONS IN NPP.  ALT ALREADY APPLIES TO RBUTTON IN ITS OWN WAY.
 !+LBUTTON::SEND !+{LBUTTON} ;SHIFTED ALT CLICK IS SPECIAL IN NPP (NOT ^).  ~RETURN LESS RELIABLE.

       !UP::SEND  ^{UP}   ;V1 NEEDS SEND.  ALT+SHIFT+UP IS ALREADY DEFINED IN NOTEPAD++, BUT NOT REALLY ALT+UP.   
     !DOWN::SEND  ^{DOWN}  
     !LEFT::SEND  ^{LEFT} ;JUMPS BY A WORD LEFT & RIGHT. 
    !+LEFT::SEND ^+{LEFT} ;JUMPS SELECTION BY A WORD LEFT & RIGHT. NOTEPAD++ BUG: IMPOSSIBLE TO SELECT A SINGLE SPACEBAR UP/DOWN OVER SEVERAL LINES, USING LALT & NO MOUSE. I CAN'T THINK OF A SOLUTION.
    !RIGHT::SEND  ^{RIGHT} 
   !+RIGHT::SEND ^+{RIGHT}
   
      !F11::F11                      ;!F11/12 CAN DO WHATEVER F11/12 SHOULD DO.
      !F12::F12                      
       F12::WINSET STYLE,^0XC40000,A ;TOGGLE GRIP & TITLEBAR.  
      +F12::
      ^F12::WINSET STYLE, ^0X40000,A ;F12 TOGGLES VIDEO TITLE-BAR, BUT ^F12 TOGGLES GRIP.  F12 IS POST-IT IN NOTEPAD++.  #g FOR WIN-GRIP IS ALREADY TAKEN!
      
        !q::SEND  ^q  ;COMMENT TOGGLE (NPP).  TOP ROW ALTS.  RHS IS CASE SENSITIVE.  FAILS ^+q BLOCK COMMENT.  AN ALTERNATIVE IS TO MAKE ALL THESE LEFTIES, <!. BUT ON LINUX < NOTATION INVALID.
       !+q::SEND ^+q  ;SHIFTED VERSIONS RARELY USED. V2 DOESN'T HAVE THIS PROBLEM.
        !e::
        ^e::SEND  #e  ;EXPLORER.
        !r::
        ^r::SEND {F2} ;RENAME.
        !t::          ;TRANSPARENCY.
        ^t::
        #t::TRANSPARENCY_TOGGLE()
        !u::SEND  ^u  ;URL OPEN (SMPLAYER).
       !+u::SEND ^+u
        !o::SEND  ^o  ;OPEN.
       !+o::SEND ^+o
       
        !a::SEND  ^a  ;ALL.  MIDDLE ROW.
       !+a::SEND ^+a 
        !s::SEND  ^s  ;SAVE.  DVORAK USERS ALSO COPY/PASTE WITH 2 HANDS.  APPLIES TO s d f z c v. POSSIBLY x.
       !+s::SEND ^+s 
        !d::  
        ^d::DUPLICATE()
       !+d::SEND ^+d  ;DUPLICATE SHIFTED.
      ^!+d::DVORAK_TOGGLE()
        !f::SEND  ^f  ;FIND.
       !+f::SEND ^+f 
       
        !z::SEND  ^z  ;UNDO.  BOTTOM ROW.
       !+z::SEND ^+z  ;REDO.
        !x::SEND  ^x  ;CUT.  
       !+x::SEND ^+x 
        !c::SEND  ^c  ;COPY. 
       !+c::SEND ^+c 
        !v::SEND  ^v  ;PASTE.
       !+v::SEND ^+v 
       
#IF DVORAK            ;V2 USES HOTIF.  
        `;::s         
       !`;::SEND ^s   ;SAVE.  HOME ROW OUT, BUT NOT IN!  ` IS THE AHK ESCAPE CHAR.  DVORAK USERS ALSO COPY/PASTE WITH 2 HANDS. APPLIES TO s d f z c v. POSSIBLY x.
         h::d         ;DUPLICATE, SHIFTED TOO.   
        !h::          
        ^h::DUPLICATE()
         y::f         
        !y::SEND ^f   ;FIND.
                      
         /::z         
        !/::SEND  ^z  ;UNDO.
       !+/::SEND ^+z  ;REDO.  BOTTOM ROW.  / & ; INVALID ON LINUX.
         b::x         ;CUT.
         i::c         
        !i::SEND ^c   ;COPY, BUT ITALIC IN WORDPAD.
         .::v         
        !.::SEND ^v   ;PASTE.
        
#IF DVORAK AND NOT(GETKEYSTATE("CTRL") OR GETKEYSTATE("ALT"))  ;DON'T CORRUPT CONTROLS, EXCEPT FOR COPY PASTE ETC.
         -::[  ;DIGITS ROW.  DVORAK.
         =::]
          
         q::'  ;QWERTY ROW. CASE SENSITIVE.
         w::,
         e::.
         r::p
         t::y
       ; y::f  ;FIND.
         u::g
       ; i::c  ;COPY.
         o::r
         p::l
         [::/
         ]::=
          
       ; a::a  ;HOME ROW. 
         s::o
         d::e
         f::u
         g::i
       ; h::d  ;DUPLICATE.
         j::h
         k::t
         l::n
         ;::s  ;SAVE.
         '::-
          
         z::;  ;BOTTOM ROW. 
         x::q
         c::j
         v::k
       ; b::x  ;CUT.
         n::b
       ; m::m
         ,::w
       ; .::v  ;PASTE.
       ; /::z  ;UNDO & REDO.

