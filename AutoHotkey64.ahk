;;PRESS RWIN FOR LOCKWORKSTATION, !r RENAME, !e EXPLORER, !d DUPLICATE, !^+d DVORAK, ^t TRANSPARENCY, ^SPACE ALWAYSONTOP, ^!SPACE SUSPEND/PAUSE, F12 TITLEBAR & F11 FOR WINDOW-LIST.  ^F12 FOR GRIP, +F11 HIDDEN WINDOWS & ^F11 FOR CONTROL-LIST.
;;THIS SCRIPT GENERALLY EMULATES CTRL WITH ALT (!c/!v FOR COPY/PASTE, ETC).  MOUSE TOO!  SOME FUNCTIONS ARE MORE TRULY ALTS.  MY LEFT PINKY IS A BIT WEAK SINCE IT BROKE @SCHOOL WHEN I WAS YOUNG. 
;;IN VIRTUALBOX CAN DISABLE "Auto Capture Keyboard". OR CAN USE LINUX AHK_X11, BASED ON v1.0.  IN MACOS (VMWARE) SWITCH  Command(⌘) & Control(^)‎  MODIFIER KEYS.  FOR BRACKETS SET:  Select All,Save,Undo,Cut,Copy,Paste = ‎⌥A,⌥O,⌥;,⌥J,⌥K,⌥Q‎  &  Redo,Open Recent... = ‎⌥⇧;,⌥⇧O‎  IN  Keyboard→Shortcuts→App Shortcuts→All Applications→+.  UNLOCKED VMWARE PERFORMS BETTER.
;;~300 LINES & ~2000 WORDS.  COMMENTS DON'T EXIST IN FINAL EXECUTABLE, BUT EVERY COMMAND/FUNCTION IS REPEATED VERBATIM.

#SINGLEINSTANCE
#MAXTHREADSBUFFER      ;DEFAULT OFF. MAXTHREADS DEFAULT IS 10. #DIRECTIVES DON'T USUALLY REQUIRE BRACKETS FOR ARGUMENTS.
#MAXTHREADSPERHOTKEY 4 ;DEFAULT 1.
SENDLEVEL 100          ;TEST.

DVORAK                 := TRUE               ;SWITCH TO FALSE FOR NO DVORAK BY DEFAULT.  V2 REQUIRES :=.  
A_ALLOWMAINWINDOW      := 1                  ;FOR BUILD.  V2 USES A_ALLOWMAINWINDOW, A_ICONTIP & A_TRAYMENU.
TRY A_TRAYMENU.DEFAULT := "&RELOAD SCRIPT"   ;FOR DEBUGGING.  MUST USE " NOT '.  
CATCH 
    A_TRAYMENU.DEFAULT := "&SUSPEND HOTKEYS" ;FOR BUILD.
A_ICONTIP              := "DOUBLE-CLICK FOR DEFAULT"

A_TRAYMENU.INSERT "&OPEN","ALWAYSONTOP TOGGLE  `t^SPACE",ALWAYSONTOP_TOGGLE ;& MEANS @ HERE.  A MONOSPACE FONT WOULD BE PREFERRED, LIKE CONSOLAS OR COURIER NEW.  `t TABS ARE TOO MUCH.  ALWAYSONTOP REQUIRES ALTTABING TO WINDOW, WHICH HAS A POSSIBLE GLITCH IN WIN10 (SOMETIMES THE MENU IS SUPPRESSED, SOMETIMES IT AIN'T).
A_TRAYMENU.INSERT "&OPEN","TITLEBAR TOGGLE     `t F12  ",TITLEBAR_TOGGLE   
A_TRAYMENU.INSERT "&OPEN","GRIP TOGGLE         `t^F12  ",TITLEBAR_TOGGLE    ;ALLOWS RESIZING WITHOUT ANNOYING TITLEBAR. BUT IT CHANGES HORIZONTAL SIZE, SLIGHTLY.
A_TRAYMENU.INSERT "&OPEN","TRANSPARENCY TOGGLE `t^t    ",TRANSPARENCY_TOGGLE
A_TRAYMENU.INSERT "&OPEN","DVORAK TOGGLE       `t!^+d  ",DVORAK_TOGGLE      ;CHECKBOX.
A_TRAYMENU.INSERT "&OPEN","WINGETLIST          `t F11  ",WINGET_LIST        ;SIMPLE LIST OF WINDOWS.
A_TRAYMENU.INSERT "&OPEN","WINGETLIST, FULL    `t+F11  ",WINGET_LIST_FULL   ;ALL WINDOWS.
A_TRAYMENU.INSERT "&OPEN","WINGETCONTROLS      `t^F11  ",WINGET_CONTROLS    ;SIMPLE LIST OF WINDOW CONTROLS.
A_TRAYMENU.INSERT "&OPEN","LockWorkStation     `t RWIN ",LockWorkStation     
A_TRAYMENU.INSERT "&OPEN"                                                   ;DIVIDERS.  
A_TRAYMENU.INSERT "&OPEN"                                                   

DVORAK_TOGGLE(*){  ;@A_TRAYMENU & @!^+d.
    GLOBAL DVORAK:=!DVORAK  
    A_TRAYMENU.TOGGLECHECK "DVORAK TOGGLE       `t!^+d  "
}

LockWorkStation(*){  ;@A_TRAYMENU.  (*)=(ITEMNAME,ITEMPOS,A_TRAYMENU)
    DLLCALL "LockWorkStation"  ;DOESN'T GENERALIZE TO LINUX, BUT #l IS DODGY.  USER32.DLL ENTRIES ARE CASE SENSITIVE. 
}

ALTTAB(){  ;@DVORAKTOGGLE, @ALWAYSONTOPTOGGLE, @TITLEBARTOGGLE, @TRANSPARENCYTOGGLE, & @CONTROLLIST.  THERE'S AN ALTTAB GLITCH FOR RE-FOCUSSING ON ACTIVE WINDOW, FROM A_TRAYMENU.
    DETECTHIDDENWINDOWS FALSE
    FOR ID IN WINGETLIST() {  ;OPTIONAL CHECK LOOP.  CHECK IF THERE'S A VISIBLE WINDOW TO !TAB TO, OTHERWISE IT RESTORES A MINIMIZED WIN.
        CLASS := WINGETCLASS(ID)
        IF  WINGETMINMAX(ID)!=-1 AND NOT INSTR(CLASS,"HIDDEN") AND (INSTR(CLASS,"CABINET") OR "explorer.exe"!=WINGETPROCESSNAME(ID)) {  ;-1=MINIMIZED.  WIN NOT MINIMIZED NOR OF A HIDDEN CLASS NOR EXPLORER.EXE, UNLESS IT'S A CABINET. A HIDDEN CLASS USUALLY HAS A BLANK TITLE, BUT SOME LEGIT WINDOWS MAY ALSO HAVE A BLANK TITLE.
            SEND "{BLIND}{ALTTABMENUDISMISS}!{TAB}{ALTTABMENUDISMISS}"  ;UNFORTUNATELY THIS IS STILL A GLITCH (ALT-TAB MENU).  {BLIND} PERFORMED BETTER IN SOME TESTS.
            BREAK
    }   }
    WINWAITACTIVE ,,,,"Task Switching"  ;FILTER OUT Task Switching.  THIS DOESN'T WAIT LONG ENOUGH ON SOME WINDOWS.
}

ALWAYSONTOP_TOGGLE(*) {  ;@A_TRAYMENU.
    ALTTAB
    WINSETALWAYSONTOP -1,"A"  ;A ACTIVE
}

TITLEBAR_TOGGLE(ITEMNAME:=FALSE,ITEMPOS:=FALSE,*){  ;@A_TRAYMENU & @*F12.
    IF ITEMPOS 
        ALTTAB
    WINGETPOS    &X,,&W,&H,"A"  ;Y COORD ALREADY PERFECT. TOGGLING GRIP CAUSES A PROBLEM WHICH FATTENS UP VIDEOS.
    WINGETCLIENTPOS ,,&W1,,"A"  ;CAN USE WIDTH TO CALCULATE OFFSET.
    
    IF INSTR(ITEMNAME,"GRIP")
         WINSETSTYLE  "^0X40000","A" 
    ELSE WINSETSTYLE "^0XC40000","A"  ;TOGGLE MORE THAN GRIP, TOO. C BEATS 8 IN MY TESTS, BUT EITHER WORKS. 0XC00000==WS_CAPTION==WS_BORDER|WS_DLGFRAME   WS_SIZEBOX==WS_THICKFRAME TOGGLES SIZING.  ^0X# IS XOR (BINARY FLAG TOGGLE)
    
    WINGETCLIENTPOS ,,&W2,,"A"
    OFFSET := (W2-W1)/2  ;CORRECTS CHANGE IN CLIENTPOS DUE TO TOGGLE.  UNFORTUNATELY SOMETIMES OFF BY 1p.
    WINMOVE X+OFFSET,,W-OFFSET*2,H-OFFSET,"A"
}

TRANSPARENCY_TOGGLE(ITEMNAME:=FALSE,*){  ;@A_TRAYMENU & @*t.
    IF   ITEMNAME
         ALTTAB 
    IF   WINGETTRANSPARENT("A")!=128 ;"" EQUALS "OFF" IN THIS CASE.
         WINSETTRANSPARENT 128,"A"              ;"A" ACTIVE
    ELSE WINSETTRANSPARENT  "","A"
}

WINGET_CONTROLS(ITEMNAME:=FALSE, *){  ;@A_TRAYMENU & @^F11.
    IF ITEMNAME
        ALTTAB
    MSG         := ""
    CONTROLLIST := WINGETCONTROLS("A")
    FOR CONTROL IN CONTROLLIST
        TRY {
            CONTROLGETPOS &X,&Y,&W,&H,CONTROL,"A"
            MSG .= FORMAT("{: 2} {}{}`t{: 9}  {: 11}`t{:.1024}`n", A_INDEX, CONTROLGETENABLED(CONTROL,"A")?"":"DISABLED`t", CONTROL, FORMAT("{}-{}",X,Y), FORMAT("W{} H{}",W,H), CONTROLGETTEXT(CONTROL,"A"))  ;1024 CHAR LIMIT ON CONTROLTEXT.
        }
        CATCH
            MSG .= CONTROL . "`n"
    MSGBOX  MSG, CONTROLLIST.LENGTH . " WIN CONTROLS FOR " . WINGETPROCESSNAME("A") . " PID " . WINGETPID("A")
}

WINGET_LIST(*){  ;@A_TRAYMENU & @F11.
    DETECTHIDDENWINDOWS FALSE  ;ACCESS DENIED IF TRUE.
    MSG := "ACTIVE?`n# PID ID MIN/MAX  X-Y  WIDTH HEIGHT  NAME.EXE  CLASS  PARENT?.EXE`nPATH.EXE`nTITLE?"
    FOR ID IN WINGETLIST() {
        PID := WINGETPID(ID)
        WINGETPOS &X,&Y,&W,&H, ID   ;X & Y CAN BE NONSENSE FOR MINIMIZED WINDOWS, X<-10000. X & Y CAN BE NEGATIVE & CORRECT, THOUGH.
        MSG .= FORMAT("`n`n{}{: 2} {: 5} {: 8} {}`t{: 9}  {: 11}`t{}    {}", WINACTIVE(ID)?"ACTIVE`n":"", A_INDEX, PID, ID, ["MIN","","MAX"][WINGETMINMAX(ID)+2], FORMAT("{}-{}",X<-1024?"?":X,Y<-1024?"?":Y), FORMAT("W{} H{}",W,H), PROCESSGETNAME(PID), WINGETCLASS(ID))
        TRY MSG .= "`t" . PROCESSGETNAME(PROCESSGETPARENT(PID))  ;PARENT TOO!
        MSG .= FORMAT("`n{}`n{}", PROCESSGETPATH(PID), WINGETTITLE(ID))
    }
    COUNT := WINGETCOUNT()
    DETECTHIDDENWINDOWS TRUE
    MSGBOX MSG, FORMAT("{} WINDOWS + {} HIDDEN!", COUNT, WINGETCOUNT()-COUNT)
}

WINGET_LIST_FULL(*){  ;@A_TRAYMENU & @+F11.
    DETECTHIDDENWINDOWS TRUE  ;ACCESS DENIED IF TRUE.
    MSG := ""
    FOR ID IN WINGETLIST()
           MSG .= FORMAT("{: 3} {}`t", A_INDEX, WINGETCLASS(ID))
    MSGBOX MSG, WINGETCOUNT() . " WINDOWS TOTAL.  THEIR # & CLASS BELOW."
}

DUPLICATE(){  ;@*d & @*h.  DUPLICATES OUTSIDE NOTEPAD++.  BUT ALSO ^d=DOUBLE-PASTES ON EMPTY SELECTION, OUTSIDE EXPLORER.  
    PROCESSNAME := WINGETPROCESSNAME("A")
    IF INSTR(PROCESSNAME,"NOTEPAD++") ;NOT CASE SENSITIVE.
        SEND "^D"                     ;LINEDUPLICATE INSIDE NPP.  "^d" ISN'T AS GOOD.
    ELSE {
        SEND "^c^v"
        IF NOT INSTR(PROCESSNAME,"EXPLORER") ;EXPLORER PASTES ONCE ONLY.  2 SENDS REQUIRED FOR RELIABILITY, OR ELSE USE CUT, NOT COPY.
            SEND "^v"                  
}}

#SUSPENDEXEMPT  ;V2 REQUIRES THIS DIRECTIVE FOR SUSPENSION TOGGLE.
!^SPACE::{
    SUSPEND
    PAUSE -1  ;TOGGLES.  V2 USES } INSTEAD OF RETURN.
}
#SUSPENDEXEMPT FALSE

    ^SPACE::
    !SPACE::WINSETALWAYSONTOP -1,"A" ;^ IS CTRL. A IS ACTIVE WINDOW. V2 TOGGLES WITH -1.
    ~*RWIN::l                        ;~ PASSES #.  RWIN IS >#  * (WILDCARD MOD) IS NECESSARY FOR SHIFT+RWIN TO TRIGGER, ALONG WITH OTHER MODS.

  !WHEELUP::^WHEELUP   ;MOUSE SECTION.  !MOUSE=^MOUSE USUALLY.  ALT-ZOOM.  NONE OF THESE WORK IN LINUX WITHOUT MORE LINES.  
!WHEELDOWN::^WHEELDOWN
  !LBUTTON::^LBUTTON   ;ALT FILE SELECTIONS, LIKE CTRL. COMPATIBLE WITH EXPLORER & NOTEPAD++.  SEND "^{LBUTTON}", LIKE V1, RUINS SQUARE SELECTIONS IN NPP.  ALT ALREADY APPLIES TO RBUTTON IN ITS OWN WAY.  
 !+LBUTTON::!+LBUTTON   ;SHIFTED ALT CLICK IS SPECIAL IN NPP (NOT ^).  ~RETURN LESS RELIABLE FOR MOUSE.
                        
       !UP::^UP         ;^UP ALT FOR NPP.
     !DOWN::^DOWN       
     !LEFT::^LEFT       ;JUMPS BY A WORD LEFT & RIGHT.  SELECTION TOO.
    !RIGHT::^RIGHT  

  ~WHEELUP::    ;~PASSTHROUGHS.  BUGFIX FOR WHEELUP/DOWN AFTER !WHEELUP/DOWN.
~WHEELDOWN::    
     ~!+UP::    ;NPP SHIFTED CTRL FLIPS LINES AROUND, BUT ALT IS LESS INTRUSIVE.  
   ~!+DOWN::RETURN    

       F11::WINGET_LIST
      !F11::F11             ;!F11 CAN DO WHATEVER F11 SHOULD DO.
      +F11::WINGET_LIST_FULL
      ^F11::WINGET_CONTROLS ;F10 MAY BE FOR EXPERIMENTS. LIKE F10::MSGBOX WINGETTEXT("A")  ;WINGETTEXT IS TOO MUCH FOR F11.
       F12::TITLEBAR_TOGGLE 
      !F12::F12                    ;!F12 CAN DO WHATEVER F12 SHOULD DO.
      +F12::
      ^F12::TITLEBAR_TOGGLE "GRIP" ;F12 TOGGLES VIDEO TITLE-BAR, BUT ^F12 TOGGLES GRIP. 0X800000 IS WS_BORDER. F12 IS POST-IT IN NOTEPAD++.

        !q::^q  ;COMMENT TOGGLE (NPP).  TOP ROW ALTS.  DIRECT MAPPINGS PRESERVE SHIFTS. SEND REQUIRES MORE LINES.  AN ALTERNATIVE IS TO MAKE ALL THESE LEFTIES, <!. BUT ON LINUX < NOTATION INVALID.
        !e::#e  ;WINDOWS EXPLORER.
        ^e::#e   
        !r::F2  ;RENAME.
        ^r::F2
        !t::
        ^t::TRANSPARENCY_TOGGLE
        !u::^u  ;URL OPEN (SMPLAYER).  (I ALSO SET +TAB FOR THIS.)
        !o::^o  ;OPEN.
                
        !a::^a  ;ALL.  HOME ROW.
        !s::^s  ;SAVE.
        !d::  
        ^d::DUPLICATE
       !+d::^+d ;SHIFT DUPLICATE.  NOT COVERED BY !d.
      !^+d::DVORAK_TOGGLE 
        !f::^f  ;FIND
        
        !z::^z  ;UNDO/REDO.  BOTTOM ROW.
        !x::^x  ;CUT.
        !c::^c  ;COPY.
        !v::^v  ;PASTE.
       
#HOTIF DVORAK   ;V2 USES HOTIF.
       !`;::^s  ;SAVE.  HOME ROW OUT, BUT NOT IN!  ` IS THE AHK ESCAPE CHAR.  DVORAK USERS ALSO COPY/PASTE WITH 2 HANDS. APPLIES TO s d f z c v. POSSIBLY x.
        `;:: s  
        !h::
        ^h::DUPLICATE 
         h:: d  ;DUPLICATE, SHIFTED TOO.   
        !y::^f  ;FIND.
         y:: f
        
        !/::^z  ;UNDO/REDO.  BOTTOM ROW.  / & ; INVALID ON LINUX.
         /:: z
         b:: x  ;CUT.
        !i::^c  ;COPY, BUT ITALIC IN WORDPAD.
         i:: c
        !.::^v  ;PASTE.
         .:: v
         
#HOTIF DVORAK AND NOT(GETKEYSTATE("CTRL") OR GETKEYSTATE("ALT"))  ;DON'T CORRUPT CONTROLS, EXCEPT FOR COPY PASTE ETC.
         -::[  ;DVORAK DIGITS ROW. 
         =::]
          
         q::'  ;TOP ROW. CASE SENSITIVE.
         w::,
         e::.
         r::p
         t::y
       ; y::f  ;FIND.
         u::g
       ; i::c  ;COPY.
         o::r
         p::l
         [::/
         ]::=
          
       ; a::a  ;HOME ROW. 
         s::o
         d::e
         f::u
         g::i
       ; h::d  ;DUPLICATE.
         j::h
         k::t
         l::n
         ;::s  ;SAVE.
         '::-
          
         z::;  ;BOTTOM ROW. 
         x::q
         c::j
         v::k
       ; b::x  ;CUT.
         n::b
       ; m::m
         ,::w
       ; .::v  ;PASTE.
       ; /::z  ;UNDO & REDO.


;; COMMENTS SECTION.  CAN TEST WINSETSTYLE & EXSTYLE FLAGS.
; F1::WINSETSTYLE   "^0X1", "A" ;CHECK WINSETSTYLE "^FLAGS"    MAX FLAG 0X80000000. HOLD DOWN ALT&SHIFT&DOWN & INSERT 00 TO CYCLE THROUGH EVERY CONCEIVABLE FLAG.
; F2::WINSETSTYLE   "^0X2", "A" 
; F3::WINSETSTYLE   "^0X4", "A" 
; F4::WINSETSTYLE   "^0X8", "A" 
; F5::WINSETSTYLE   "^0X10", "A"
; F6::WINSETSTYLE   "^0X20", "A"
; F7::WINSETSTYLE   "^0X40", "A"
; F8::WINSETSTYLE   "^0X80", "A"
; F1::WINSETEXSTYLE "^0X1" , "A"  ;CHECK WINSETEXSTYLE "^FLAGS"    SAME AS ABOVE BUT WITH "EX"

;; MAYBE !4 & !5 COULD CLOSE & REFRESH, BUT NOT WORTH RISK. ALSO POSSIBLE TO MAKE ALT+HOME_ROW INTO DIGITS.

