;;HOTKEYS:  LockWorkStation RWIN  TRANSPARENCY #t  EXPLORER !e  RENAME !r  DUPLICATE ^d  DVORAK ^!+d  ALWAYSONTOP ^SPACE  SUSPEND ^!SPACE  TITLEBAR F12  GRIP ^F12  WINDOW-LIST F11  HIDDEN-WINDOWS +F11  CONTROLLIST ^F11  .
;;THIS SCRIPT GENERALLY EMULATES CTRL WITH ALT (!c/!v FOR COPY/PASTE, ETC).  MOUSE TOO!  
;;~300 LINES & ~2000 WORDS.  THIS SCRIPT IS STRIPPED OF COMMENTS AND SENT TO \.rsrc\RCDATA\1  (OPEN .EXE IN 7-ZIP).  

DVORAK := TRUE  ;SWITCH TO FALSE FOR NO DVORAK BY DEFAULT.  V2 REQUIRES :=.  MORE LAYOUTS COULD BE ADDED, TOO.

#SINGLEINSTANCE
A                      := "A"                ;FOR V2. "A"=ACTIVE.
A_ALLOWMAINWINDOW      := 1                  ;FOR COMPILATION.  V2 USES A_ALLOWMAINWINDOW, A_ICONTIP & A_TRAYMENU.
TRY A_TRAYMENU.DEFAULT := "&RELOAD SCRIPT"   ;FOR DEBUGGING.  MUST USE " NOT '.  
CATCH 
    A_TRAYMENU.DEFAULT := "&SUSPEND HOTKEYS" ;FOR COMPILATION.
A_ICONTIP              := FORMAT("AHKVersion {}`n{} @DOUBLE-CLICK",A_AHKVERSION,A_TRAYMENU.DEFAULT)  ;`n,`t=LINE,TAB

A_TRAYMENU.INSERT "&OPEN","ALWAYSONTOP TOGGLE  `t #SPACE",ALWAYSONTOP_TOGGLE ;& MEANS @, HERE.  A MONOSPACE FONT WOULD BE PREFERRED, LIKE CONSOLAS OR COURIER NEW.  `t TABS ARE TOO MUCH.  ALWAYSONTOP REQUIRES ALTTABING TO WINDOW, WHICH HAS A POSSIBLE GLITCH IN WIN10 (SOMETIMES THE MENU IS SUPPRESSED, SOMETIMES IT AIN'T).
A_TRAYMENU.INSERT "&OPEN","TRANSPARENCY TOGGLE `t #t    ",TRANSPARENCY_TOGGLE
A_TRAYMENU.INSERT "&OPEN","TITLEBAR TOGGLE     `t  F12  ",TITLEBAR_TOGGLE   
A_TRAYMENU.INSERT "&OPEN","GRIP TOGGLE         `t ^F12  ",TITLEBAR_TOGGLE    ;^ IS CTRL. ALLOWS RESIZING WITHOUT ANNOYING TITLEBAR. BUT IT CHANGES HORIZONTAL SIZE, SLIGHTLY.
A_TRAYMENU.INSERT "&OPEN","DVORAK TOGGLE       `t ^!+d  ",DVORAK_TOGGLE      ;HAS CHECKBOX.  ! IS ALT. + IS SHIFT.  
A_TRAYMENU.INSERT "&OPEN"                                                    ;DIVIDER.  
A_TRAYMENU.INSERT "&OPEN","WINGETLIST          `t  F11  ",WINGET_LIST        ;SIMPLE LIST OF WINDOWS.
A_TRAYMENU.INSERT "&OPEN","WINGETLIST, FULL    `t +F11  ",WINGET_LIST_FULL   ;ALL WINDOWS.
A_TRAYMENU.INSERT "&OPEN","WINGETCONTROLS      `t ^F11  ",WINGET_CONTROLS    ;SIMPLE LIST OF WINDOW CONTROLS.
A_TRAYMENU.INSERT "&OPEN"                                                 
A_TRAYMENU.INSERT "&OPEN","DUPLICATE           `t  !d   ",DUPLICATE          ;NON-TRIVIAL EXAMPLE.  !e & !r ARE TRIVIAL.
A_TRAYMENU.INSERT "&OPEN","LockWorkStation     `t  RWIN ",LockWorkStation     
A_TRAYMENU.INSERT "&OPEN"                                                 
A_TRAYMENU.INSERT "&OPEN"                                                   

DVORAK_TOGGLE(*){  ;@A_TRAYMENU & @^!+d.  V2 REQUIRES (*)=(ITEMNAME,ITEMPOS,A_TRAYMENU)
    GLOBAL DVORAK := !DVORAK  
    A_TRAYMENU.TOGGLECHECK "DVORAK TOGGLE       `t ^!+d  "
}

LockWorkStation(*){  ;@A_TRAYMENU.  
    DLLCALL "LockWorkStation"  ;USER32.DLL ENTRIES ARE CASE SENSITIVE.  INVALID ON LINUX, BUT #l IS DODGY.
}

ALTTAB(){  ;@DUPLICATE, @DVORAKTOGGLE, @ALWAYSONTOPTOGGLE, @TITLEBARTOGGLE, @TRANSPARENCYTOGGLE, & @CONTROLLIST.  
    SEND "{BLIND}!{TAB}{ALTTABMENUDISMISS}"   ;{BLIND} MORE RELIABLE!  UNFORTUNATELY THIS IS STILL A GLITCH (ALTTAB MENU).  THIS RESTORES A MINIMIZED WIN IF THEY'RE ALL MINIMIZED, FOR SIMPLICITY.  
    WINWAITACTIVE ,,,,"Task Switching" ;FILTERS OUT Task Switching.
}

DUPLICATE(ITEMNAME:=FALSE, *){  ;@A_TRAYMENU, @*d & @*h.  DUPLICATES OUTSIDE NOTEPAD++.  BUT ALSO ^d=DOUBLE-PASTES ON EMPTY SELECTION, OUTSIDE EXPLORER.  
    IF ITEMNAME
        ALTTAB
    PROCESSNAME   := WINGETPROCESSNAME(A)
    IF PROCESSNAME = "NOTEPAD++.EXE" ;NOT CASE SENSITIVE.
        SEND     "^D"                ;LINEDUPLICATE INSIDE NPP.  "^d" ISN'T AS GOOD (DEPENDING).
    ELSE {
        SEND "^c"                                             ;2 SENDS MORE RELIABLE ON LINUX.  LINUX CHANGES HOW ALL SCRIPTS ARE WRITTEN. 
        SENDVAR := PROCESSNAME="EXPLORER.EXE" ? "^v" : "^v^v" ;EXPLORER PASTES ONCE ONLY. 
        SEND SENDVAR
}}

ALWAYSONTOP_TOGGLE(*) {  ;@A_TRAYMENU.
    ALTTAB
    WINSETALWAYSONTOP -1,A
}

TITLEBAR_TOGGLE(ITEMNAME:=FALSE,ITEMPOS:=FALSE,*){  ;@A_TRAYMENU & @*F12.
    IF ITEMPOS 
        ALTTAB
    STYLE := INSTR(ITEMNAME,"GRIP") ? "^0X40000" : "^0XC40000"   ;TOGGLE GRIP OR ELSE GRIP+TITLEBAR.  C BEATS 8 IN MY TESTS, BUT EITHER WORKS. 0XC00000==WS_CAPTION==WS_BORDER|WS_DLGFRAME   WS_SIZEBOX==WS_THICKFRAME TOGGLES SIZING.  ^0X# IS XOR (BINARY FLAG TOGGLE)
    
    WINGETPOS     &X,,&W ,&H ,A  ;Y COORD ALREADY PERFECT. TOGGLING GRIP CAUSES A PROBLEM WHICH FATTENS UP VIDEOS.
    WINGETCLIENTPOS ,,&W1,   ,A  ;CAN USE WIDTH TO CALCULATE OFFSET.
    WINSETSTYLE     STYLE    ,A  
    WINGETCLIENTPOS ,,&W2,   ,A
    
    OFFSET := (W2-W1)/2  ;CORRECTS CHANGE IN CLIENTPOS DUE TO TOGGLE.  UNFORTUNATELY OFF BY 1p.
    WINMOVE X+OFFSET,,W-OFFSET*2,H-OFFSET,A
}

TRANSPARENCY_TOGGLE(ITEMNAME:=FALSE,*){  ;@A_TRAYMENU & @*t.  A DIFFERENT CONCEPT IS TO RUN THIS ON AN AUTO-TIMER, SO A FILM KEEPS FLIPPING TRANSPARENCY.
    IF ITEMNAME
        ALTTAB 
    ALPHA := WINGETTRANSPARENT(A) ? "OFF" : 128  ;""=FALSE="OFF"=255 (BUT MORE EFFICIENT).  INVERT TRANSPARENCY STATE.
    WINSETTRANSPARENT ALPHA,   A
}

WINGET_CONTROLS(ITEMNAME:=FALSE, *){  ;@A_TRAYMENU & @^F11.
    IF ITEMNAME
        ALTTAB
    MSG         := ""
    CONTROLLIST := WINGETCONTROLS(A)
    FOR CONTROL IN CONTROLLIST {
        CONTROLGETPOS &X,&Y,&W,&H,    CONTROL,A
        DISABLED := CONTROLGETENABLED(CONTROL,A)?"":"DISABLED"
        MSG      .= FORMAT("{} {} {}`t X,Y,W,H={},{},{},{}`t TEXT={:.64}`n",A_INDEX,DISABLED,CONTROL,X,Y,W,H,CONTROLGETTEXT(CONTROL,A))  ;1024 CHAR LIMIT ON CONTROLTEXT.
    }
    MSGBOX  MSG,FORMAT("{} CONTROLS FOR {}  PID={}  HWND={}",CONTROLLIST.LENGTH,WINGETPROCESSNAME(A),WINGETPID(A),WINGETPID(A),WINACTIVE(A))
}

WINGET_LIST(*){  ;@A_TRAYMENU & @F11.  A DIFFERENT CONCEPT IS TO UPDATE THIS USING A TIMER. A WINDOW MONITOR CAN BE LIKE TASK MANAGER. THIS TAKES A SNAPSHOT, INSTEAD.
    DETECTHIDDENWINDOWS FALSE 
    MSG         := ""
    ACTIVEHWND  := WINACTIVE(A)  ;ACTIVE HWND.
    LIST        := WINGETLIST()
    FOR HWND IN LIST {
        WINGETPOS &X,&Y,&W,&H,   HWND  ;X & Y CAN BE NEGATIVE.  NONSENSE IF MINIMIZED (X<-10000).
        PID     := WINGETPID(    HWND )
        MINMAX  := WINGETMINMAX( HWND )
        XY      := MINMAX = -1         ? "" : FORMAT("X,Y={},{}",X,Y)
        ACTIVE  := HWND  != ACTIVEHWND ? "" : "ACTIVE`t "
        MINMAX  := ["MINIMIZED","","MAXIMIZED"][MINMAX+2]
        MSG     .= FORMAT("{}: {}{}`t {}`n{}`t CLASS={}`t PID={}`t {} {} W,H={},{}`t HWND={}",A_INDEX,ACTIVE,WINGETTITLE(HWND),PROCESSGETPATH(PID),PROCESSGETNAME(PID),WINGETCLASS(HWND),PID,MINMAX,XY,W,H,HWND)  ;CAPS ARE LIKE FLAGS.
        TRY MSG .= FORMAT("`t PARENT={}",PROCESSGETNAME(PROCESSGETPARENT(PID)))  ;PARENT TOO!
        MSG     .=        "`n`n"
    }
    DETECTHIDDENWINDOWS TRUE
    MSGBOX MSG, FORMAT("{} WINDOWS, +{} HIDDEN!",LIST.LENGTH,WINGETCOUNT()-LIST.LENGTH)
}

WINGET_LIST_FULL(*){  ;@A_TRAYMENU & @+F11.
    DETECTHIDDENWINDOWS TRUE
    MSG     := ""
    LIST    := WINGETLIST()
    FOR HWND IN LIST
        MSG .= FORMAT("{} {}  ",A_INDEX,WINGETCLASS(HWND))
    MSGBOX MSG,LIST.LENGTH . " WINDOWS TOTAL. CLASSES BELOW."  ;~340 IN WIN-10.
}

#SUSPENDEXEMPT       ;V2 REQUIRES THIS DIRECTIVE.
   ^!SPACE::SUSPEND  ;S-SPACE-SUSPEND
#SUSPENDEXEMPT FALSE
    ^SPACE::
    !SPACE::
    #SPACE::WINSETALWAYSONTOP -1,A ;V2 TOGGLES WITH -1.
    ~*RWIN::l                      ;l LOCK.  ~ PASSES #.  RWIN IS >#.  * (WILDCARD MOD) IS NECESSARY FOR SHIFT+RWIN TO TRIGGER, ALONG WITH OTHER MODS.  RWIN-LOCK CAN BE ANNOYING IF A PASSWORD IS LONG OR UNKNOWN. BUT IT COULD ALSO PREVENT SEIZURE OF A SYSTEM WHICH IS TOO DIFFICULT TO LOCK.

  !WHEELUP::^WHEELUP   ;MOUSE SECTION, !MOUSE=^MOUSE.  ALT-ZOOM.
!WHEELDOWN::^WHEELDOWN
  !LBUTTON::^LBUTTON   ;ALT FILE SELECTIONS, LIKE CTRL. COMPATIBLE WITH EXPLORER & NOTEPAD++.  SEND "^{LBUTTON}", LIKE V1, RUINS SQUARE SELECTIONS IN NPP.  ALT ALREADY APPLIES TO RBUTTON IN ITS OWN WAY.  
 !+LBUTTON::!+LBUTTON  ;SHIFTED ALT CLICK IS SPECIAL IN NPP (NOT ^).  ~RETURN LESS RELIABLE FOR MOUSE.
                       
       !UP::^UP        ;^UP ALT FOR NPP.
     !DOWN::^DOWN      
     !LEFT::^LEFT      ;JUMPS BY A WORD LEFT & RIGHT.  SELECTION TOO.
    !RIGHT::^RIGHT  

  ~WHEELUP::           ;~PASSTHROUGHS.  BUGFIX FOR WHEELUP/DOWN AFTER !WHEELUP/DOWN.
~WHEELDOWN::           
     ~!+UP::           ;NPP SHIFTED CTRL FLIPS LINES AROUND, BUT ALT IS LESS INTRUSIVE.  
   ~!+DOWN::RETURN    

       F11::WINGET_LIST
      +F11::WINGET_LIST_FULL
      ^F11::WINGET_CONTROLS ;F10 MAY BE FOR EXPERIMENTS. LIKE F10::MSGBOX WINGETTEXT(A)  ;WINGETTEXT IS TOO MUCH FOR F11.
       F12::TITLEBAR_TOGGLE 
      +F12::
      ^F12::TITLEBAR_TOGGLE "GRIP" ;F12 TOGGLES VIDEO TITLE-BAR, BUT ^F12 TOGGLES GRIP. 0X800000 IS WS_BORDER. F12 IS POST-IT IN NOTEPAD++.  #g FOR WIN-GRIP IS ALREADY TAKEN!
      !F11::F11                    ;!F11/12 CAN DO WHATEVER F11/12 SHOULD DO.
      !F12::F12
      
        !q::^q  ;q COMMENT TOGGLE (NPP).  TOP ROW ALTS.  DIRECT MAPPINGS PRESERVE SHIFTS. SEND REQUIRES MORE LINES.  AN ALTERNATIVE IS TO MAKE ALL THESE LEFTIES, <!. BUT ON LINUX < NOTATION INVALID.
        !e::#e  ;e EXPLORER.
        ^e::#e   
        !r::F2  ;r RENAME.  IDEALLY THIS WOULD SEND ENTER, IF ALREADY RENAMING.
        ^r::F2
        !t::    ;t TRANSPARENCY.
        ^t::
        #t::TRANSPARENCY_TOGGLE
        !u::^u  ;u URL OPEN (SMPLAYER).  (I ALSO SET +TAB FOR THIS.)
        !o::^o  ;o OPEN.
                
        !a::^a  ;a ALL.  HOME ROW.
        !s::^s  ;s SAVE.
        !d::  
        ^d::DUPLICATE
       !+d::^+d ;d SHIFT DUPLICATE.  NOT COVERED BY !d.
      ^!+d::DVORAK_TOGGLE 
        !f::^f  ;f FIND
        
        !z::^z  ;z UNDO/REDO.  BOTTOM ROW.
        !x::^x  ;x CUT.
        !c::^c  ;c COPY.
        !v::^v  ;v PASTE.
       
#HOTIF DVORAK   ;  V2 USES HOTIF.  #d=#e ALSO.
       !`;::^s  ;s SAVE.  HOME ROW OUT, BUT NOT IN!  ` IS THE AHK ESCAPE CHAR.  DVORAK USERS ALSO COPY/PASTE WITH 2 HANDS. APPLIES TO s d f z c x v.
        `;:: s  
        !h::
        ^h::DUPLICATE 
         h:: d  ;d DUPLICATE, SHIFTED TOO, +LINEDELETE.   
        !y::^f  ;f FIND.
         y:: f
       
        !/::^z  ;z UNDO/REDO.  BOTTOM ROW.  
         /:: z
        !b::^x  ;x CUT.  REMOVE THIS LINE FOR !b=BOLD.
         b:: x 
        !i::^c  ;c COPY. REMOVE THIS LINE FOR !i=ITALIC, IN WORDPAD.
         i:: c
        !.::^v  ;v PASTE.
         .:: v
         
#HOTIF DVORAK AND NOT(GETKEYSTATE("CTRL") OR GETKEYSTATE("ALT"))  ;DON'T CORRUPT CONTROLS, EXCEPT FOR COPY PASTE ETC.
         -::[  ;DIGITS ROW.  DVORAK.
         =::]
          
         q::'  ;QWERTY ROW. CASE SENSITIVE.
         w::,
         e::.
         r::p  ;Р=R & ту NEXT TO EACH OTHER, LIKE RUSSIAN: АБЧДЭФГХИЖКЛМНОП РСТУВ  ЙЗЕЁ ЫЮ Я Ц ШЩ ЬЪ.
         t::y
       ; y::f  ;FIND.
         u::g
       ; i::c  ;COPY.
         o::r
         p::l  ;#p LockWorkStation.  rl ARE THE "WRONG" WAY AROUND IN DVORAK.
         [::/
         ]::=
          
       ; a::a  ;HOME ROW. 
         s::o
         d::e  ;#d EXPLORER.
         f::u
         g::i
       ; h::d  ;DUPLICATE.
         j::h
         k::t
         l::n
         ;::s  ;SAVE.
         '::-
          
         z::;  ;BOTTOM ROW. 
         x::q
         c::j
         v::k
       ; b::x  ;CUT.
         n::b
       ; m::m
         ,::w
       ; .::v  ;PASTE.
       ; /::z  ;UNDO & REDO.


;;COMMENTS SECTION.  
;;IN VIRTUALBOX CAN DISABLE "Auto Capture Keyboard". OR CAN USE LINUX ahk_x11, BASED ON v1.0.  IN MACOS-VMWARE SWITCH  Command(⌘) & Control(^)‎  MODIFIER KEYS.  FOR BRACKETS.IO SET:  Select All,Save,Undo,Cut,Copy,Paste = ‎⌥A,⌥O,⌥;,⌥J,⌥K,⌥Q‎  &  Redo,Open Recent... = ‎⌥⇧;,⌥⇧O‎  IN Keyboard→Shortcuts→App Shortcuts→All Applications→+.  UNLOCKED VMWARE PERFORMS BETTER.
;;I DON'T ACTUALLY KNOW WHERE THE KEYS ARE ON MY OWN KEYBOARD, WITHOUT TYPING A WORD. LIKE XAVIER FOR X. CONTROL-KEY MEMORY IS DIFFERENT TO TOUCH-TYPING.

;;CAN TEST WINSETSTYLE & WINSETEXSTYLE FLAGS. CYCLE TRHOUGH ALL POWERS OF 2. MAX FLAG 0X80000000.
; F1::WINSETSTYLE   "^0X1" ,A  
; F2::WINSETSTYLE   "^0X2" ,A 
; F3::WINSETSTYLE   "^0X4" ,A 
; F4::WINSETSTYLE   "^0X8" ,A 
; F5::WINSETSTYLE   "^0X10",A
; F6::WINSETSTYLE   "^0X20",A
; F7::WINSETSTYLE   "^0X40",A
; F8::WINSETSTYLE   "^0X80",A
; F1::WINSETEXSTYLE "^0X1" ,A  ;ETC.

;; MAYBE !4 & !5 COULD CLOSE & REFRESH, BUT NOT WORTH RISK. ALSO POSSIBLE TO MAKE ALT+HOME_ROW INTO DIGITS.

